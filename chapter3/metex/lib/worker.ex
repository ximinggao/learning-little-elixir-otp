defmodule Metex.Worker do
  @spec loop :: no_return
  def loop do
    receive do
      {sender_pid, location} ->
        send(sender_pid, {:ok, temperature_of(location)})

      _ ->
        IO.puts("don't know how to process this message")
    end

    loop()
  end

  @spec temperature_of(binary) :: <<_::40, _::_*8>>
  def temperature_of(location) do
    result = url_for(location) |> HTTPoison.get() |> parse_response()

    case result do
      {:ok, temp} -> "#{location}: #{temp}Â°C"
      :error -> "#{location} not found"
    end
  end

  defp url_for(location) do
    location = URI.encode(location)
    "http://api.openweathermap.org/data/2.5/weather?q=#{location}&appid=#{apikey()}"
  end

  defp parse_response({:ok, %HTTPoison.Response{body: body, status_code: 200}}) do
    body |> Jason.decode!() |> compute_temperature()
  end

  defp parse_response(_) do
    :error
  end

  defp compute_temperature(json) do
    try do
      temp = (json["main"]["temp"] - 273.15) |> Float.round(1)
      {:ok, temp}
    rescue
      _ -> :error
    end
  end

  defp apikey do
    "3964b5dda26f70a453130f87ff3afae3"
  end
end
